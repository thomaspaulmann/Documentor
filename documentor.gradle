def getExcludes() {
    return hasProperty('DOCUMENTOR_EXCLUDE') ? DOCUMENTOR_EXCLUDE : ""
}

def getDestinationDir() {
    return hasProperty('DOCUMENTOR_DESTINATION_DIR') ? DOCUMENTOR_DESTINATION_DIR : "../javadoc/"
}

task generateJavadoc(type: Javadoc) {
    // Setup Javadoc
    source         = android.sourceSets.main.java.srcDirs
    destinationDir = file(getDestinationDir())
    classpath     += project.files(android.getBootClasspath().join(File.pathSeparator))
    group          = "documentor"
    description    = "Generate your awesome Javadocs!"

    // Setup standard links to Java + Android documentation
    options {
        links("http://docs.oracle.com/javase/7/docs/api/")
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")

        if (JavaVersion.current().isJava8Compatible()) {
            addStringOption('Xdoclint:none', '-quiet')
        }
    }

    // Exclude Android specific files
    exclude '**/BuildConfig.java', '**/R.java'

    // Exclude configured files
    exclude getExcludes().split(',')
}
