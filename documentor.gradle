def getDocumentorExcludes() {
    return hasProperty('DOCUMENTOR_EXCLUDES') ? DOCUMENTOR_EXCLUDES : ""
}

def getDocumentorOutputDir() {
    return hasProperty('DOCUMENTOR_OUTPUT_DIR') ? DOCUMENTOR_OUTPUT_DIR : "../documentation/"
}

task document(type: Javadoc) {
    // Setup Javadoc
    source         = android.sourceSets.main.java.srcDirs
    destinationDir = file(getDocumentorOutputDir())
    classpath     += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath     += files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
    group          = "documentor"
    description    = "Generate your awesome Documentation!"

    // Setup standard links to Java + Android documentation
    options {
        links("http://docs.oracle.com/javase/7/docs/api/")
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")

        // Disable doclint
        if (JavaVersion.current().isJava8Compatible()) {
            addStringOption('Xdoclint:none', '-quiet')
        }
    }

    // Exclude Android specific files
    exclude '**/BuildConfig.java', '**/R.java'

    // Exclude configured files
    exclude getDocumentorExcludes().split(',')
}
